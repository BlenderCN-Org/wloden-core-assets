import shaderSignature;
import environment;
import fragment;
import terrainMaterial;
import lighting;

function fetchAlbedoPlane(texcoord: float2, splatting: float4) -> float4
{
    using MaterialState;

    let a0 = albedoSampler.sample(albedoTexture0, texcoord*textureScales[0] + textureOffsets[0]);
    let a1 = albedoSampler.sample(albedoTexture1, texcoord*textureScales[1] + textureOffsets[1]);
    let a2 = albedoSampler.sample(albedoTexture2, texcoord*textureScales[2] + textureOffsets[2]);
    let a3 = albedoSampler.sample(albedoTexture3, texcoord*textureScales[3] + textureOffsets[3]);
    let a4 = albedoSampler.sample(albedoTexture4, texcoord*textureScales[4] + textureOffsets[4]);

    return mixSplatting(a0, a1, a2, a3, a4, splatting);
}

function mixTriplanar(normal: float3, vx: float4, vy: float4, vz: float4) -> float4
{
    let blending = normalize(fabs(normal));
    blending = blending / (blending.x + blending.y + blending.z);

    return vx*blending.x + vy*blending.y + vz*blending.z;
}

function fetchAlbedo(objectPosition: float3, normal: float3, splatting: float4) -> float4
{
    let vx = fetchAlbedoPlane(objectPosition.yz, splatting);
    let vy = fetchAlbedoPlane(objectPosition.xz, splatting);
    let vz = fetchAlbedoPlane(objectPosition.xy, splatting);

    return mixTriplanar(normal, vx, vy, vz);
}

function fetchNormalPlane(texcoord: float2, splatting: float4) -> float4
{
    using MaterialState;

    let n0 = normalSampler.sample(normalTexture0, texcoord*textureScales[0] + textureOffsets[0]);
    let n1 = normalSampler.sample(normalTexture1, texcoord*textureScales[1] + textureOffsets[1]);
    let n2 = normalSampler.sample(normalTexture2, texcoord*textureScales[2] + textureOffsets[2]);
    let n3 = normalSampler.sample(normalTexture3, texcoord*textureScales[3] + textureOffsets[3]);
    let n4 = normalSampler.sample(normalTexture4, texcoord*textureScales[4] + textureOffsets[4]);

    return mixSplatting(n0, n1, n2, n3, n4, splatting);
}

function fetchNormal(objectPosition: float3, normal: float3, splatting: float4) -> float4
{
    let vx = fetchNormalPlane(objectPosition.yz, splatting);
    let vy = fetchNormalPlane(objectPosition.xz, splatting);
    let vz = fetchNormalPlane(objectPosition.xy, splatting);

    return mixTriplanar(normal, vx, vy, vz);
}

code_block(fragment) main
{
    let dh = 1.0f / float2(getTextureSize(heightMapTexture));

    let surfaceTexcoord = TerrainFragmentInput.surfaceTexcoord;
    let objectPosition = TerrainFragmentInput.objectPosition;

    if(surfaceTexcoord.s < 0.0f || surfaceTexcoord.t < 0.0f || surfaceTexcoord.s > 1.0f || surfaceTexcoord.t > 1.0f)
        discard;

    // Fetch the splatting
    let splatting = albedoSampler.sample(splattingTexture, surfaceTexcoord);

    // Compute the height map derivatives
    let dx = displacementSampler.sample(heightMapTexture, surfaceTexcoord + float2(dh.x*0.5f, 0.0f)).r - displacementSampler.sample(heightMapTexture, surfaceTexcoord - float2(dh.x*0.5f, 0.0f)).r;
    let dy = displacementSampler.sample(heightMapTexture, surfaceTexcoord + float2(0.0f, dh.y*0.5f)).r - displacementSampler.sample(heightMapTexture, surfaceTexcoord - float2(0.0f, dh.y*0.5f)).r;

    let ot = normalize(float3(dh.x, dx*MaterialState.heightScale, 0.0f));
    let ob = normalize(float3(0.0f, dy*MaterialState.heightScale, -dh.y));
    let on = normalize(cross(ot, ob));

    // Build the TBN matrix
    let t = transformNormalToView(ot);
    let b = transformNormalToView(ob);
    let n = transformNormalToView(on);
    let TBN = float3x3(t, b, n);

    // Fetch the albedo.
    let rawAlbedo = fetchAlbedo(objectPosition, on, splatting);
    let albedo = TerrainFragmentInput.color*MaterialState.albedo*float4(rawAlbedo.rgb, 1.0f);

    // Fetch the actual normal
    let rawNormal = fetchNormal(objectPosition, on, splatting);
    let normalMapValue = rawNormal.xyz*2.0f - 1.0f;
    let N = normalize(TBN*normalMapValue);

    // Compute the smoothness and the fresnel factor.
    let fresnel = MaterialState.fresnel*rawAlbedo.a;
    let smoothness = MaterialState.smoothness*rawNormal.a;

    let V = normalize(-TerrainFragmentInput.position);

    color: FragmentOutput.color <== forwardLightingModel
        albedo: albedo
        fresnel: fresnel smoothness: smoothness
        normal: N viewVector: V position: TerrainFragmentInput.position;
}
