import environment;

function fresnelSchlick(F0: float3, cosTheta: float) -> float3
{
    let powFactor = 1.0f - cosTheta;
    let powFactor2 = powFactor * powFactor;
    let powFactor4 = powFactor2 * powFactor2;
    let powValue = powFactor4 + powFactor;

    return F0 + (float3(1.0f, 1.0f, 1.0f) - F0) * powValue;
}

code_block forwardLightingModel
{
    in normal: float3;
    in viewVector: float3;
    in position: float3;
    in albedo: float4;
    in smoothness: float;
    in fresnel: float3;
    out color: float4;

    using GlobalLightingState;

    let albedoColor = albedo.rgb;

    let specularPower = exp2(10.0f*smoothness);
    let specularNormalization = (specularPower + 2.0f) * 0.125f;

    let accumulatedColor = float3(0.0f, 0.0f, 0.0f);

    // Add the ambient hemispherical light.
    let hemiFactor = dot(normal, sunDirection)*0.5f + 0.5f;
    accumulatedColor += albedoColor * mix(groundLighting.rgb, skyLighting.rgb, hemiFactor);

    // Accumulate each one of the lights
    for(let i = 0; i < numberOfLights; i += 1)
    {
        let lightSource = lightSources[i];
        let L = lightSources[i].position.xyz - position*lightSource.position.w;
        let dist = length(L);
        L = L / dist;

        let NdotL = max(dot(normal, L), 0.0f);
        if(NdotL == 0.0f)
            continue;

        // Are we inside the spot light cone?
        let spotCos = 1.0f;
        if(lightSource.outerCosCutoff > -1.0f) // For no spot lights.
            spotCos = dot(L, lightSource.spotDirection);

        if(spotCos < lightSource.outerCosCutoff)
            continue;
        let spotAttenuation = smoothstep(lightSource.outerCosCutoff, lightSource.innerCosCutoff, spotCos)*pow(spotCos, lightSource.spotExponent);

        // Compute the attenuation factor.
        // I am using (partly) the technique that I found here: https://imdoingitwrong.wordpress.com/2011/01/31/light-attenuation/
        let attenuationDistance = max(0.0f, dist - lightSource.radius);
        let attDen = 1.0f + attenuationDistance/lightSource.radius;
        let attenuation = spotAttenuation/(attDen*attDen);

        // Compute the fresnel factor
        let H = normalize(L + viewVector);
        let F = fresnelSchlick(fresnel, dot(H, L));

        // Compute the distribution factor.
        let NdotH = dot(normal, H);
        let D = pow(NdotH, specularPower)*specularNormalization;

        // Add the light contribution.
        accumulatedColor += lightSource.intensity.rgb*attenuation*(albedoColor + F*D)*NdotL;
    }

    // Return the final lighted color.
    color = float4(accumulatedColor, albedo.a);
}
